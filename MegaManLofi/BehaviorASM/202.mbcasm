##########################################################################
#
# 202: Spinning Turret
#
# Every 0.25 seconds, this enemy should fire in the next diagonal direction
#
##########################################################################
#
# Float Registers:
#
# [0] - elapsed seconds since last shoot call
# [1] - seconds to wait before shooting
# [2] - frame seconds
#
# Int Registers:
#
# [0] - is first run (0 is yes, 1 is no)
# [1] - direction counter
# [2] - direction increment value (1)
# [3] - total directions (8, use this to reset direction counter)
#
# [10] - Direction::Left (0)
# [11] - Direction::UpLeft (1)
# [12] - Direction::Up (2)
# [13] - Direction::UpRight (3)
# [14] - Direction::Right (4)
# [15] - Direction::DownRight (5)
# [16] - Direction::Down (6)
# [17] - Direction::DownLeft (7)
#
##########################################################################

FUNC main

	# init directions on first run
	if_eqi 0 0
		call init
		regi 0 1
	endif

	# increment elapsed seconds
	get_frameseconds 2
	addf 0 2 0

	if_gtf 0 1

		# shoot, then move to the next direction
		shoot
		addi 1 2 1
		if_gtei 1 3
			regi 1 0
		endif
		set_direction 1

		# reset timer
		regf 0 0

	endif

RET

FUNC init

	# seconds to wait before shooting
	regf 1 0.25

	# direction registers
	regi 10 0  # Left
	regi 11 1  # UpLeft
	regi 12 2  # Up
	regi 13 3  # UpRight
	regi 14 4  # Right
	regi 15 5  # DownRight
	regi 16 6  # Down
	regi 17 7  # DownLeft

	regi 2 1  # direction increment value
	regi 3 8  # total directions

RET